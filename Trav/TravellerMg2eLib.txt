--/|ScriptCards Library: TravellerMg2eLib 
--/|Provides various utility procedures for Mongoose Traveller 2e games
--/|NOTES: Some procedures require TokenMod to be installed, see the ScriptCards WIKI for procedure documentation

--/| LIST OF ELEMENTS THAT DONT WORK WHEN CALLED FROM THE LIBRARY
--/|sourceToken|@{selected|token_id}  <-- not able to find the selected token

--/| Variables:  =/$ are Roll variables (numbers)
--/| Variables:  &/& are string variables (text)
--/| Note: use String variable (&/&) to store a dice string without evaluating it. Then use [&varname] in the Roll variable (=/$) evaluate the dice roll



--/|--------------------------------------------------
--/|  Format the script card chat output style
--/|--------------------------------------------------
--:LibTrav_CardHeader|ReturnVariableName;InputValue
  --/|CARD STYLING
  --#titlecardbackground|#000000
  --#tableBorder|3px solid #8B0000;
  --#evenrowbackground|#D3D3D3
  --#oddrowbackground|#E5E4E2
  --#titleFontColor|#FFD700
  --#noMinMaxHighlight|1
  --#noRollHighlight|1
--<|

--/|--------------------------------------------------
--/|  Toggle a Status Marker On or Off on a Token
--/|--------------------------------------------------
--:LibTrav_ToggleTokenStatusMarker|StatusOnOff;TokenStatusMarkerName
  --/| DEFINE THE RETURN VARIABLE
    --&[%1%]|

  --/| TOGGLE THE MARKER WITH TOKENMOD
    --/| Give Players ability to run tokenmod    
    --@token-mod|_config players-can-ids|on
    --/| Toggle the marker
    --@token-mod|_set statusmarkers|![%2%]

  --/|TEST IF TOKEN STATUS MARKER IS SET
    --/| if not set, index will = "ArrayError"
    --~[StatusMarks]|array;statusmarkers;StatusMarks;[*S:t-id]
    --~MarkerIndex|array;indexof;StatusMarks;[%2%]
    --/Marker Index|[&MarkerIndex]
    --?[&MarkerIndex] -ne  "ArrayError" |[
      --/Cleared Token Marker!|
      --&[%1%]|Off
    --]|[
      --/Set Token Marker!|
      --&[%1%]|On
    --]|
--<|

--/|--------------------------------------------------
--/|  Check if a Status Marker is Present on a Token
--/|--------------------------------------------------
--:LibTrav_CheckTokenStatusMarker|StatusOnOff_s;TokenStatusMarkerName_s
  --/| DEFINE THE RETURN VARIABLE AS STRING
    --&[%1%]|
  --/|TEST IF TOKEN STATUS MARKER IS SET
    --/| if not set, index will = "ArrayError"
    --~[StatusMarks]|array;statusmarkers;StatusMarks;[*S:t-id]
    --~MarkerIndex|array;indexof;StatusMarks;[%2%]
    --/Marker Index|[&MarkerIndex]
    --?[&MarkerIndex] -eq  "ArrayError" |[
      --/Token Marker Off|[%2%]
      --&[%1%]|Off
    --]|[
      --/Token Marker On|[%2%]
      --&[%1%]|On
    --]|
--<|


--/|--------------------------------------------------
--/|  Return values (or 0) of the 3 token bubbles in an array
--/|  Blank Bubbles = 0
--/|  Usage @BubbleValueRGB_l(0) = Red Bubble Value
--/|--------------------------------------------------
--:LibTrav_ReadTokenBubblesRGB|BubbleValueRGB_l
  --/|READ THE BUBBLE VALUES IN DISPLAY ORDER
    --=Red3Value_i|[*S:t-bar3_value]
    --=Grn1Value_i|[*S:t-bar1_value]
    --=Blu2Value_i|[*S:t-bar2_value]
  --/| DEFINE THE RETURN VARIABLE AS ARRAY
    --&[%1%]|
    --~|array;define;[%1%];[$Red3Value_i];[$Grn1Value_i];[$Blu2Value_i]
  --/| FOR DEBUG - PRINT ARRAY
    --%loop|foreach;[%1%]
      --+[%1%]|[&loop]
    --%|
--<|


--/|--------------------------------------------------
--/|  Toggle Token Status Marker and Print definition
--/|    emoteState = 1 hides the emote ssection
--/|    hideTitleCard = 1 hide card title
--/|--------------------------------------------------
--:LibTrav_TokenMarkerDefinition|ReturnVariableName;StatusOnOff;Icon;Title;Definition
    --#title|[%3%] [%4%] [%3%]
    --?[%2%] -eq "Off" |[
        --#hideCard|1
    --]|[
        --+[%3%]|[%5%]
    --]|
--<|

